cmake_minimum_required(VERSION 3.0)
project(kde2-i18n)
find_package(kdelibs2 REQUIRED)
find_package(Gettext REQUIRED)

function(install_language LANGNAME)
    set(LANGPATH ${LANGNAME}/messages/)
    install(FILES
        ${LANGPATH}/charset
        ${LANGPATH}/entry.desktop
        ${LANGPATH}/flag.png
        DESTINATION "${KDE2_LOCALE}/${LANGNAME}")
    file(GLOB_RECURSE POS RELATIVE "${CMAKE_CURRENT_SOURCE_DIR}/${LANGPATH}" "*.po")
    list(FILTER POS EXCLUDE REGEX "\\.\\./" )

    foreach(PO ${POS})
        set(PO "${LANGPATH}${PO}")
        get_filename_component(BASENAME ${PO} NAME_WE)
        get_filename_component(DIR ${PO} DIRECTORY)
        set(PO "${CMAKE_CURRENT_SOURCE_DIR}/${DIR}/${BASENAME}.po")
        set(MO "${CMAKE_CURRENT_BINARY_DIR}/${DIR}/${BASENAME}.mo")
        file(MAKE_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/${DIR}")
        list(APPEND MOS ${MO})
        add_custom_command(OUTPUT ${MO}
            COMMAND ${GETTEXT_MSGFMT_EXECUTABLE} -o ${MO} ${PO}
            WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}"
            DEPENDS ${PO}
            )
    endforeach()
    add_custom_target(${LANGNAME} ALL DEPENDS ${MOS})
    install(FILES ${MOS} DESTINATION ${KDE2_LOCALE}/${LANGNAME}/LC_MESSAGES/)
endfunction()

install_language(af)
install_language(az)
install_language(bg)
install_language(br)
install_language(bs)
install_language(ca)
install_language(cy)
install_language(cs)
install_language(da)
install_language(de)
install_language(el)
install_language(en_GB)
install_language(eo)
install_language(es)
install_language(et)
install_language(eu)
install_language(fi)
install_language(fr)
install_language(ga)
install_language(gl)
install_language(he)
install_language(hr)
install_language(hu)
install_language(id)
install_language(is)
install_language(it)
install_language(ja)
install_language(ko)
install_language(lt)
install_language(lv)
install_language(mi)
install_language(mk)
install_language(nl)
install_language(no)
install_language(no_NY)
install_language(oc)
install_language(pl)
install_language(pt)
install_language(pt_BR)
install_language(ro)
install_language(ru)
install_language(sk)
install_language(sl)
install_language(sv)
install_language(ta)
install_language(vi)
install_language(wa)

# TODO: has a broken PO
# Apparently the standard isn't as strict anymore, and GNU's msgfmt decided it
# was fun to put more work on the translators.
#install_language(sr)
#install_language(th)
#install_language(tr)
#install_language(uk)
#install_language(mt)
#install_language(xh)
#install_language(zh_CN.GB2312)
#install_language(zh_TW.Big5)
